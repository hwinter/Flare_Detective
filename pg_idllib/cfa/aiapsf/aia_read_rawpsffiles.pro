;+
; NAME:
;
; aia_read_rawpsffiles
;
; PURPOSE:
;
; Reads PSF files generated by ZEMAX ray-tracing for AIA telescopes.
; This is a low-level routine that requires explicit filename input.
;
; CATEGORY:
;
; SDO/AIA calibration - PSF tools
;
; CALLING SEQUENCE:
;
; psf=aia_read_rawpsffiles(filename,header=header)
;
; INPUTS:
;
; filename: filename for the raw PSF files generated by ZEMAX software
;
; OPTIONAL INPUTS:
;
; NONE 
;
; KEYWORD PARAMETERS:
;
; NONE
;
; OUTPUTS:
;
; psf: a 2D float array with the PSF, or -1 if an error occurred
;
; OPTIONAL OUTPUTS:
;
; header: string array with the information in the file headers
;
; COMMON BLOCKS:
;
; NONE
;
; SIDE EFFECTS:
;
; NONE
;
; RESTRICTIONS:
;
; Needs a valid PSF file as input.
;
; PROCEDURE:
;
; Read & parse file.
;
; EXAMPLE:
;
; filename='/home/pgrigis/AIA_PSF_Data/T3_UV/T3_Huygens_PSF_for_1600A_Field_1.txt'
; psf=aia_read_rawpsffiles(filename,header=header,x=x,y=y)
; tvscl,alog(0.1+psf)
;
; AUTHOR:
;
; Paolo Grigis
; pgrigis@cfa.harvard.edu
;
; MODIFICATION HISTORY:
;
; 13-JAN-2009 written PG
;
;-


FUNCTION aia_read_rawpsffiles,filename,header=header,x=x,y=y

IF NOT file_exist(filename) THEN BEGIN 
   print,'File '+filename+' does not exist!'
   RETURN,-1
ENDIF


;read string array with the file content
rawdata=rd_ascii(filename)

;this part is the header
header=rawdata[0:17]


;size of the PSF in the files - assumes 128x128 for now
nx=128
ny=128

;create empty PSF
data=fltarr(nx,ny)

;define pitch of PSF and AIA pixel size
psfpitch=2.0;micron
aiapixelsize=12.0;micron

;x-array and y-array - position of the pixel in units of AIA CCD size
x=(findgen(nx)-nx/2+(nx MOD 2 EQ 0? 0.5 : 0.0))*psfpitch/aiapixelsize
y=(findgen(ny)-ny/2+(ny MOD 2 EQ 0? 0.5 : 0.0))*psfpitch/aiapixelsize


FOR i=18,nx+17 DO BEGIN 
   data[*,i-18]=float(strsplit(rawdata[i],' ',/extract))
ENDFOR

RETURN,data

END


