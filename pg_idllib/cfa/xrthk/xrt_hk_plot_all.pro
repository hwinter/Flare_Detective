;+
; NAME:
;
;   xrt_hk_plot_all
;
; PURPOSE:
;
;   Main program to plot housekeeping values
;
; CATEGORY:
;
;   XRT HK utils
;
; CALLING SEQUENCE:
;
;   xrt_hk_plot_all,time=time,tagfile=tagfile,hk_2colfiledir=hk_2colfiledir,hk_outimdir=hk_outimdir
;
; INPUTS:
;
;
; OPTIONAL INPUTS:
;
;   time:           housekeeping plots are generated for 7 days, 24 hours, 6 hours and 3
;                   hours before time. Default is now.
;   hk_2colfiledir: root directory that contains the two column files with the HK
;                   values at SAO. Default: '/data/solarb/XRT/hk_cron/'
;   hk_outimdir:    root directory in which to save housekeeping plots. 
;                   Default: '/data/solarb/XRT/hk_pro/images/'
;   tagfile:        File with the list of all HK values files to read in.
;                   Deafult='/data/solarb/XRT/hk_pro/programs/keywords_kkr.txt'
;
; KEYWORD PARAMETERS:
;
;  verbose:  If set, prints more information on the screen
;  no1val:   If set, prevents the creation of the one value plots
;  nogroup:  If set, prevents the creation of the group plots
;
; OUTPUTS:
;
;
;
; OPTIONAL OUTPUTS:
;
;
;
; COMMON BLOCKS:
;
;   NONE 
;
; SIDE EFFECTS:
;
;
;
; RESTRICTIONS:
;
;
;
; PROCEDURE:
;
;   Reads in the HK value and plots them.
;
; EXAMPLE:
;
; xrt_hk_plot_all,hk_outimdir='~/machd/hktrish/gifs/' 
;
; AUTHOR
;
; Paolo C. Grigis  
; pgrigis@cfa.harvard.edu
;
; MODIFICATION HISTORY:
;
;   17-NOV-2008 written PG (based on a routine by P. Jibben)
;   18-NOV-2008 added option to select only one value or group plots PG
;
;-

PRO xrt_hk_plot_all,time=intime,tagfile=tagfile,hkfiledir=hkfiledir,hk_outimdir=hk_outimdir $
                   ,no1val=no1val,nogroup=nogroup,verbose=verbose

;Two columns ASCII files with HK values lives in subdirs of
hkfiledir=fcheck(hkfiledir,'/data/solarb/XRT/hk_cron/')

;Images generated by this program are stored in subdirs of
hk_outimdir=fcheck(hk_outimdir,'/data/solarb/XRT/hk_pro/images/')

;tag file: list of keyword names to read in
tagfile=fcheck(tagfile,'/data/solarb/XRT/hk_pro/programs/keywords_kkr.txt')
readcol,tagfile, hktag, format='A'

;Plots runs until this time. Default is now in UT.
time=fcheck(intime,anytim(systime(1,/utc))+anytim('01-JAN-1970'))

dtime=86400.0

;set plot properties

cs=1.0    ;charsize

loadct,0  
!p.color=0 ;color black
!p.background=255;background white
wdef,1,1024,768;window size

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;do single temp plots

IF NOT keyword_set(no1val) THEN BEGIN 

   ;loop over all HK values
   FOR i=0, n_elements(hktag)-1 DO BEGIN

      ;reads in 24 hours worth of HK values
      hkdata=xrt_hk_getdata(hktag[i],time-dtime,time,hkfiledir=hkfiledir)

      ;check if valid data has been found
      IF size(hkdata,/TNAME) EQ 'STRUCT' THEN BEGIN 

         ;plot valid data

         IF keyword_set(verbose) THEN print,'Plotting '+hktag[i]+' 3, 12, 24 hours plots.'

         xrt_hk_plot_1val,hkdata,time,dtime=3,hk_outimdir=hk_outimdir,/save,charsize=cs,verbose=verbose
         xrt_hk_plot_1val,hkdata,time,dtime=12,hk_outimdir=hk_outimdir,/save,charsize=cs,verbose=verbose
         xrt_hk_plot_1val,hkdata,time,dtime=24,hk_outimdir=hk_outimdir,/save,charsize=cs,verbose=verbose

      ENDIF $
      ELSE BEGIN 

         ;if no data is found, write "NO DATA" on plot instead

         IF keyword_set(verbose) THEN print,'No data found for '+hktag[i]
         ptag=['03','12','24']
         plot,[0,0],/nodata,xrange=[0,1],yrange=[0,1],xtitle='Plot generated on '+!stime,title=hktag[i]
         xyouts,0.1,0.5,'NO DATA FOUND',charsize=8,/normal,charthick=4

         FOR j=0,n_elements(ptag)-1 DO BEGIN 
            filename=hk_outimdir+hktag[i]+'_'+ptag[j]+'hr.png'
            IF keyword_set(verbose) THEN print,'Saving '+filename
            tvlct,r,g,b,/get
            write_png,filename,tvrd(),r,g,b
         ENDFOR

      ENDELSE

   ENDFOR

ENDIF


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;do group plots

;Plots runs until this time. Default is now in UT.
;Computed again in case single values plots take a long time...
time=fcheck(intime,anytim(systime(1,/utc))+anytim('01-JAN-1970'))

dtime=7*24*3600L ; 7 days span for group plots

;list of the groups numbers
groups=['00','01','02', $
        '03','04','19','20','21','22', $
        '06','07','08','09', $
        '05','18', $
        '11','12','13','14','10', $
        '15','16','17']

;The indices of the different groups of HK temperatures in array "groups" 
;run 0-2 for Group 1, 3-8 for Group 2, 9-12 for group 3 etc. 
gid=[0,3,9,13,15,20,23]

;Build HK tag string
hktag='TMP'+groups+'C'

IF NOT keyword_set(nogroup) THEN BEGIN 

   ;loops over the groups
   FOR i=0,n_elements(gid)-2 DO BEGIN 

      IF keyword_set(verbose) THEN print,'Plotting GROUP '+string(i,format='(I02)')+' : 3, 12, 24, 168 hours plots.'

      ndata=gid[i+1]-gid[i]     ;number of elements in this group
      hkdatarr=ptrarr(ndata)    ;build a pointer array, one pointer for each temperature

      ;loops over elements of the group
      FOR j=0,ndata-1 DO BEGIN
         IF keyword_set(verbose) THEN print,'Reading 7 days of ',hktag[gid[i]+j]
      
         ;read HK values for each temperature
         hkdatarr[j]=ptr_new(xrt_hk_getdata(hktag[gid[i]+j],time-dtime,time,hkfiledir=hkfiledir))

      ENDFOR


      ;prepare 3,12,24 hours and 7 days (=168 hours) plots
      xrt_hk_plot_group,hkdatarr,time,dtime=3,hk_outimdir=hk_outimdir,/save,charsize=cs,thisgroup=i+1
      xrt_hk_plot_group,hkdatarr,time,dtime=12,hk_outimdir=hk_outimdir,/save,charsize=cs,thisgroup=i+1
      xrt_hk_plot_group,hkdatarr,time,dtime=24,hk_outimdir=hk_outimdir,/save,charsize=cs,thisgroup=i+1
      xrt_hk_plot_group,hkdatarr,time,dtime=168,hk_outimdir=hk_outimdir,/save,charsize=cs,thisgroup=i+1

      ;clean up to conserve memory
      ptr_free,hkdatarr

   ENDFOR
ENDIF

END



