;+
; NAME:
; 
;   pg_2dimcorr
;
; PURPOSE:
;
;  computes two dimensional cross correlation as a fucntion of x and y lag
;
; CATEGORY:
;
;  math/statistic util
;
; CALLING SEQUENCE:
;
; corrmat=pg_2dimcorr(mat1,mat2,xlag=xlag,ylag=ylag [,/double] )
;
; INPUTS:
; 
; mat1,mat2: the two matrices to corss correlate
; xlag,ylag: a number specifying the minimum and maximum lag for the cross
;            correlation, which is computed as a matrix of size 2*xlag+1 by 2*ylag+1 
;            corresponding to lag values of
;            -xlag,-xlag+1,...,-1,0,1,...,xlag-1,xlag and
;            -ylag,-ylag+1,...,-1,0,1,...,ylag-1,ylag
;            
; OPTIONAL INPUTS:
;
; 
;
; KEYWORD PARAMETERS:
;
; double: if set, the correlations are computed iobn double precision.
;
; OUTPUTS:
;
; corrmat: the correlation matrix for the lags explained above
;
; OPTIONAL OUTPUTS:
;
;
;
; COMMON BLOCKS:
;
;
;
; SIDE EFFECTS:
;
;
;
; RESTRICTIONS:
;
;
;
; PROCEDURE:
;
; The IDL procedure "correlate" is repeateadly applied to shifted input matrices.
;
; EXAMPLE:
;
;
;
; AUTHOR:
; 
;  Paolo Grigis, pgrigis@cfa.harvard.edu
;
; MODIFICATION HISTORY:
;
;  08-FEB-2008 written PG
;-


FUNCTION pg_2dimcorr,a1,a2,xlag=xlag,ylag=ylag,double=double

  xlag=fcheck(xlag,4)
  ylag=fcheck(ylag,4)
  nx=xlag*2+1
  ny=ylag*2+1
  xlagarray=indgen(nx)-xlag
  ylagarray=indgen(ny)-ylag
  IF keyword_set(double) THEN cmat=dblarr(nx,ny) $
                         ELSE cmat=fltarr(nx,ny)

  FOR i=0,nx-1 DO BEGIN 
     FOR j=0,ny-1 DO BEGIN 
        cmat[i,j]=correlate(shift(a1,xlagarray[i],ylagarray[j]),a2,double=double)
     ENDFOR
  END

  RETURN,cmat

END

