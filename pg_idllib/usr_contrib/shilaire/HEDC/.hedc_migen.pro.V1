; PURPOSE:
;	Will create image tesseract fitsfile (with good time intervals). A panel of images can later be made (hedc_tepgen.pro),
;	or imageing spectroscopy (hedc_imspec.pro).
;
;	EXAMPLE:
;		hedc_migen,'2002/02/26 '+['10:26','10:27'], [925,-225], 'tmp_imgtess',/IMSPECebands			
;
;
;
;	rootfilnam is filename WITHOUT extension!
;	ebands is a [2,n_ebands] array!
;--------------------------------------------------------------------------------------------------------------------------------------------------
PRO hedc_migen, flare_intv, xyoffset, rootfilnam, time_intvs=time_intvs, 							$
	deltat=deltat,imgalg=imgalg,detindex=detindex,pixsize=pixsize,imgdim=imgdim,ebands=ebands,IMSPECebands=IMSPECebands,	$
	imagetesseract=imagetesseract, 												$ 	;OPTIONAL OUTPUT
	eventcode=eventcode,ev_struct=ev_struct					;needed keywords for writing HEDC .info stuff...
	

;initialization phase:
	IF NOT KEYWORD_SET(deltat) THEN deltat=20. ELSE deltat=FLOAT(deltat)
	IF NOT KEYWORD_SET(imgalg) THEN imgalg='clean'
	IF NOT KEYWORD_SET(detindex) THEN detindex=[0,0,1,1,1,1,1,1,0]
	IF NOT KEYWORD_SET(pixsize) THEN pixsize=1
	IF NOT KEYWORD_SET(imgdim) THEN imgdim=128
	IF NOT KEYWORD_SET(ebands) THEN ebands=[[3,6],[6,12],[12,25],[25,50],[50,100],[100,300]]
	IF KEYWORD_SET(IMSPECebands) THEN ebands=[[4,5],[5,6],[6,7],[7,8],[8,9],[9,10],[10,12],[12,14],[14,16],[16,18],[18,20],[20,25],[25,30],[30,35],[35,40],[40,45],[45,50],[50,60],[60,70],[70,80],[80,90],[90,100]]

;local variables:
	n_ebands=N_ELEMENTS(ebands[0,*])
	
	IF KEYWORD_SET(time_intvs) THEN t_intvs=time_intvs ELSE BEGIN
		t_intvs=-1
		good_img_intvs=to_nice_img_intervals(flare_intv,mindeltat=deltat/2.,/ATTLOW,wait_after_shutter=60.,wait_after_eclipse=120.)
			;good_img_intvs=to_nice_img_intervals(flare_intv,mindeltat=deltat/2.,/ATTLOW,/PARTICLES,wait_after_shutter=60.,wait_after_eclipse=120.)
		IF N_ELEMENTS(good_img_intvs) THEN MESSAGE,'.......NO GOOD TIME INTV FOUND!'
		good_img_intvs=anytim(good_img_intvs)
		FOR i=0,N_ELEMENTS(good_img_intvs[0,*])-1 DO BEGIN
			duration=good_img_intvs[1,i]-good_img_intvs[0,i]
			n=duration/deltat	;n is a double
			WHILE n GE 1. DO BEGIN
				IF N_ELEMENTS(t_intvs) EQ 1 THEN t_intvs=good_img_intvs[1,i]-[n,n-1]*deltat ELSE t_intvs=[[t_intvs],[good_img_intvs[1,i]-[n,n-1]*deltat]]			
				n=n-1.
			ENDWHILE
			IF n GE 0.5 THEN IF N_ELEMENTS(t_intvs) EQ 1 THEN t_intvs=good_img_intvs[1,i]-[n,0]*deltat ELSE t_intvs=[[t_intvs],[good_img_intvs[1,i]-[n,0]*deltat]]			
		ENDFOR
		IF N_ELEMENTS(t_intvs) EQ 1 THEN t_intvs=good_img_intvs
	ENDELSE
	
	n_intvs=N_ELEMENTS(t_intvs[0,*])
	
;start: now the main course	
	mmo=hsi_multi_image()
	mmo->set,im_time_interval=t_intvs
	mmo->set,im_energy_bin=ebands
	ss=WHERE(ebands GT 100) & IF ss[0] EQ -1 THEN mmo->set,FRONT=1,REAR=0 ELSE mmo->set,FRONT=1,REAR=1 		;If ANY of the ebands is above 100, have to use rear detectors for ALL images...
	mmo->set,image_alg=imgalg
	mmo->set,pixel_size=pixsize,image_dim=imgdim,xyoffset=xyoffset
	mmo->set,det_index_mask=detindex
	mmo->set_no_screen_output
	mmo->set,progress_bar=0
	mmo->set,multi_fits_filename=rootfilnam+'.fits'
	imagetesseract=mmo->getdata()	

	IF KEYWORD_SET(eventcode) THEN BEGIN
		info=mmo->get()
		info.ABSOLUTE_TIME_RANGE=flare_intv
		info.ENERGY_BAND=[ebands[0,0],ebands[1,n_ebands-1]]
		hedc_write_info_file,'imsp',ev_struct,info,rootfilnam+'.info'
	ENDIF

	OBJ_DESTROY,mmo
PRINT,'...............hedc_migen.pro FINISHED without crashes!!'
END



