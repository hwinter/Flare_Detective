; V4.
; PURPOSE:
;	Finds automatically STRONG sources and makes imaging spectroscopy of them.
;	Will try to find sources in the lowest energy band, as well as in a high energy band
;	Assumes that there are actually sources! Also, only takes into account sources within 0.5 of map's Z maximum.
;	Actually, jchooser should never be <0...
;	Will display all images in a time-energy panel, with an additional panel at the bottom of each column with spectra.
;	Color of spectra correspond to color of source contour in same column...
;
; INPUTS:
;	sourceimg OR ichooser,jchooser
;		sourceimg; image (2-d array) or imagecube (in time) with as many time intv as in the fitsfile.
;		ichooser: index (in time) of image to take for source finding. If <0, will pick the current time intv.
;		jchooser: index (in energy) of image to take for source finding. Can be an array, if different energies are to be used to find sources.
;
;
; KEYWORDS:
;	fitsfile: RHESSI imagecube fitfile
;	pngfile: filename to which to output the PNG image.
;	fitEband: eband (ex: [15,100]) within which to fit the power-law.
;	disp: the energy bands to be displayed. (ex: 0, 4, or [0,2,4]...)
;	SKIP: skips some time intervals, following an algorithm which might change according to my humor... (SKIP=1, 2, 3... for different methods...)
;		
; EXAMPLE:
;	
;	hedc_imspec, -1, [1,4,7], fitsfile=fil
;	set_plot,'Z' & hedc_imspec, 2, 3,pngfile='Ebudget2/test.png', fitsfile=fil & x
;	set_plot,'Z' & hedc_imspec, -1, [1,4,7], fitsfile=fil, pngfile='Ebudget2/test.png' & x
;
;
;----------------------------------------------------------------------------------------------------------------------------------------
FUNCTION hedc_imspec_robust_linefit, x,y, yfit=yfit, sig=sig, coeff_sig=coeff_sig
	es=0
	CATCH,es
	IF es NE 0 THEN BEGIN
		CATCH,/CANCEL
		RETURN,-1	
	ENDIF
	coeff=robust_linefit(x,y,yfit1,sig1,coeff_sig1)
	yfit=yfit1
	sig=sig1
	coeff_sig=coeff_sig1
	RETURN,coeff	
END
;----------------------------------------------------------------------------------------------------------------------------------------
FUNCTION hedc_imspec_extract_sources, img, VERBOSE=VERBOSE, min_sigma=min_sigma

	IF KEYWORD_SET(min_sigma) THEN BEGIN
		; check if the map is really only noise...if it is the case, return -1
		;to do this, I'll simply try to find a value higher than min_sigma*sigmas above mean...
		IF MAX(img) LT (MEAN(img)+min_sigma*STDDEV(img)) THEN RETURN,-1
	ENDIF

	n_sources_max=6
	
	imgdim=N_ELEMENTS(img[*,0])	
	src=REPLICATE({gra:-1d,grb:-1d,tet:0d,max:-1d,xmax:0d,ymax:0d},n_sources_max)
	extract_sources, img, src, NBMAX=n_sources_max,fmax=0.5
	ss=WHERE(src.gra GE 0)
	IF ss[0] NE -1 THEN src=src[ss]	
	src.gra=1/sqrt(src.gra)
	src.grb=1/sqrt(src.grb)
	IF KEYWORD_SET(VERBOSE) THEN printai,src
	RETURN,src
END
;----------------------------------------------------------------------------------------------------------------------------------------
PRO hedc_imspec, in1,in2, fitsfile=fitsfile, pngfile=pngfile, fitEband=fitEband, STATS=STATS, disp=disp, SKIP=SKIP
	ichooser=in1
	jchooser=in2		
	IF NOT KEYWORD_SET(fitsfile) THEN fitsfile='hsi_imagetesseract_HXS202201106.fits'
	IF NOT KEYWORD_SET(fitEband) THEN fitEband=[15,100]	; for power-law
	IF NOT KEYWORD_SET(disp) THEN disp=jchooser	;disp=jchooser[N_ELEMENTS(jchooser)-1]
	
	imgtess=mrdfits(fitsfile,0)
	xdim=N_ELEMENTS(imgtess[*,0,0,0])
	ydim=N_ELEMENTS(imgtess[0,*,0,0])
	n_ebands=N_ELEMENTS(imgtess[0,0,*,0])
	n_tintvs=N_ELEMENTS(imgtess[0,0,0,*])

	tmp=mrdfits(fitsfile,1)
	tintvs=tmp.times_arr
	ebands=tmp.ebands_arr
	edge_products,ebands,mean=ebins
	
	;get Obs. Summ. countrates (corrected).
	ro=OBJ_NEW('hsi_obs_summ_rate')
	ro->set,obs_time_interval=[tintvs[0,0],tintvs[1,N_ELEMENTS(tintvs[0,*])-1]]+[-120,120]
	tmp=ro->getdata()
	ostimes=ro->get(/time)
	osrates=ro->corrected_countrate()
	OBJ_DESTROY,ro

	IF KEYWORD_SET(SKIP) THEN BEGIN
		CASE SKIP OF
			1: BEGIN		
				;if bigger than 7 intervals, take 1 out of 3.... might miss peak time!!!
				sf=3D	;skip factor
				IF n_tintvs GE 7 THEN ss=WHERE(abs(FINDGEN(n_tintvs)/sf-FLOOR(FINDGEN(n_tintvs)/sf)) LE 0.0001)	; supposes I have less then 10000 time intervals...
				imgtess=imgtess[*,*,*,ss]
				tintvs=tintvs[*,ss]
				n_tintvs=N_ELEMENTS(imgtess[0,0,0,*])
			END
			2: BEGIN
				;take all intervals up to peak time (>25 keV) + 1 minute. Take 1 out of 3 afterwards...
				tmp=MAX(TOTAL(osrates[2:*,*],1),os_peak_ss)
				os_lim_ss=os_peak_ss[0]+15
				ss1=WHERE(tintvs[1,*] LE ostimes[os_lim_ss])

				ss=WHERE(tintvs[0,*] GT ostimes[os_lim_ss])
				sf=3D	;skip factor
				IF ss[0] NE -1 THEN BEGIN
					ss2=WHERE(abs(FINDGEN(N_ELEMENTS(ss))/sf-FLOOR(FINDGEN(N_ELEMENTS(ss))/sf)) LE 0.0001) 
					IF ss2[0] NE -1 THEN ss2=ss[ss2]
				ENDIF ELSE ss2=-1
			
				IF ss1[0] EQ -1 AND ss1[0] EQ -1 THEN PRINT,'Problem with skip method -- not skipping anything...'
				IF ss1[0] EQ -1 AND ss2[0] NE -1 THEN BEGIN
					imgtess=imgtess[*,*,*,ss2]
					tintvs=tintvs[*,ss2]
				ENDIF
				IF ss1[0] NE -1 AND ss2[0] EQ -1 THEN BEGIN
					imgtess=imgtess[*,*,*,ss1]
					tintvs=tintvs[*,ss1]
				ENDIF
				IF ss1[0] NE -1 AND ss2[0] NE -1 THEN BEGIN
					tmp=[ss1,ss2]
					ss=tmp[UNIQ(tmp,SORT(tmp))]
					imgtess=imgtess[*,*,*,ss]
					tintvs=tintvs[*,ss]
				ENDIF
				n_tintvs=N_ELEMENTS(imgtess[0,0,0,*])
			END
					
			ELSE: PRINT,'Skip method not recognized. Using default (i.e. no skipping)...'
		ENDCASE
	ENDIF

	diffposmin2=4	;units are pixel^2
	diffsizemin2=4	;additional sources that have same position (within diffposmin) AND same size (within diffsizemin) are discarded (i.e. only one is taken)
	n_src_max=2	;max # of sources per eband...
	min_sigma=4	;minimum # sigmas above mean map value for a map to be considered something else than noise... 0 disables this check...
	basesize=180
	imspecplotysize=2-is_even(N_ELEMENTS(disp))
	
	n_yplots=N_ELEMENTS(disp)+imspecplotysize+1
	psh_win,basesize*n_tintvs,n_yplots*basesize
	hessi_ct
	rainbow_linecolors
	
	;if ichooser is >=0, means I only determine sources once...
	IF ichooser GE 0 THEN BEGIN
		src='blabla'
		src_eband=INTARR(N_ELEMENTS(jchooser))
		FOR j=0,N_ELEMENTS(jchooser)-1 DO BEGIN
			newsrc=hedc_imspec_extract_sources(imgtess[*,*,jchooser[j],ichooser],min_sigm=min_sigma)
			IF datatype(newsrc) EQ 'STC' THEN BEGIN
				;should remove any sources whose position and size are similar to previous sources before going on...
						k=N_ELEMENTS(newsrc)-1
						IF datatype(src) EQ 'STC' THEN goon=1 ELSE goon=0
						WHILE goon DO BEGIN
							ss=WHERE( ((src.xmax-newsrc[k].xmax)^2 LE diffposmin2) AND ((src.ymax-newsrc[k].ymax)^2 LE diffposmin2) AND ((src.gra-newsrc[k].gra)^2 LE diffsizemin2) AND ((src.grb-newsrc[k].grb)^2 LE diffsizemin2) ) 
							toosimilar=(ss[0] NE -1)
							IF toosimilar THEN BEGIN
								IF k EQ 0 THEN newsrc=-1 ELSE newsrc=newsrc[0:k-1] ;remove it
							ENDIF
							k=k-1
							IF k EQ -1 THEN goon=0
						ENDWHILE 
			ENDIF
			;going on...
			IF datatype(newsrc) EQ 'STC' THEN BEGIN
				new_n_src=N_ELEMENTS(newsrc) < n_src_max
				src_eband[j]=new_n_src
				IF datatype(src) NE 'STC' THEN src=newsrc[0:new_n_src-1] ELSE src=[src,newsrc[0:new_n_src-1]]
			ENDIF
		ENDFOR
		IF datatype(src) EQ 'STC' THEN n_src=N_ELEMENTS(src) ELSE n_src=0
	ENDIF ;//IF ichooser LT 0


	;plot the lightcurve
	!P.MULTI=[0,1,n_yplots]	
	clear_utplot
	utplot,ostimes,TOTAL(osrates[0:1,*],1),color=1,ytit='Corrected !CCts/s/det',/YLOG,yr=[1,10000],xtit='',charsize=1.5,xstyle=1,xmar=[6,1],ymar=[2,1]
	outplot,ostimes,osrates[2,*],color=2
	outplot,ostimes,TOTAL(osrates[3:4,*],1),color=3
	outplot,ostimes,TOTAL(osrates[5:*,*],1),color=4
	
	plot_label,/NOLINE,/DEV,[0.03,-1],'3-12 keV',color=1
	plot_label,/NOLINE,/DEV,[0.03,-2],'12-25 keV',color=2
	plot_label,/NOLINE,/DEV,[0.03,-3],'25-100 keV',color=3
	plot_label,/NOLINE,/DEV,[0.03,-4],'0.1-17 MeV',color=4

	;plot image intervals...
	FOR i=0L,n_tintvs-1 DO BEGIN
		PLOTS,[tintvs[0,i],tintvs[0,i]],[1,1e4],thick=5,linestyle=2,color=5
		PLOTS,[tintvs[0,i],tintvs[1,i]],[1e4,1e4],thick=5,linestyle=2,color=5
		PLOTS,[tintvs[1,i],tintvs[1,i]],[1,1e4],thick=5,linestyle=2,color=5
		PLOTS,[tintvs[0,i],tintvs[1,i]],[1,1],thick=5,linestyle=2,color=5
		;plot_label,/NOLINE,/DEV,[0.03,-1],charsize=2.0,strn(i+1)
		label_plot, tintvs[0,i]+0.2*(tintvs[1,i]-tintvs[0,i]) ,/XDATA,-1,charsize=1.5,strn(i+1)
	ENDFOR

	;MAIN LOOP:-----------------------------------------------------------------------------------------------------------------
	FOR i=0L,n_tintvs-1 DO BEGIN

		;identify sources, if ichooser was >=0. Otherwise, this has already been done before entering the main loop.-----------------------------------
		IF ichooser LT 0 THEN BEGIN
			src='blabla'
			src_eband=INTARR(N_ELEMENTS(jchooser))
			FOR j=0,N_ELEMENTS(jchooser)-1 DO BEGIN
				newsrc=hedc_imspec_extract_sources(imgtess[*,*,jchooser[j],i],min_sigm=min_sigma)
				IF datatype(newsrc) EQ 'STC' THEN BEGIN
				;should remove any sources whose position and size are similar to previous sources before going on...
					k=N_ELEMENTS(newsrc)-1
					IF datatype(src) EQ 'STC' THEN goon=1 ELSE goon=0
					WHILE goon DO BEGIN
						ss=WHERE( ((src.xmax-newsrc[k].xmax)^2 LE diffposmin2) AND ((src.ymax-newsrc[k].ymax)^2 LE diffposmin2) AND ((src.gra-newsrc[k].gra)^2 LE diffsizemin2) AND ((src.grb-newsrc[k].grb)^2 LE diffsizemin2) ) 
						toosimilar=(ss[0] NE -1)
						IF toosimilar THEN BEGIN
							IF k EQ 0 THEN newsrc=-1 ELSE newsrc=newsrc[0:k-1] ;remove it
						ENDIF
						k=k-1
						IF k EQ -1 THEN goon=0
					ENDWHILE 
				ENDIF
				;going on...
				IF datatype(newsrc) EQ 'STC' THEN BEGIN
					new_n_src=N_ELEMENTS(newsrc) < n_src_max
					src_eband[j]=new_n_src
					IF datatype(src) NE 'STC' THEN src=newsrc[0:new_n_src-1] ELSE src=[src,newsrc[0:new_n_src-1]]
				ENDIF
			ENDFOR					
			IF datatype(src) EQ 'STC' THEN n_src=N_ELEMENTS(src) ELSE n_src=0
		ENDIF

		;plot the pictures wanted------------------------------------------------------------------------------------------------------------------
		!P.MULTI=[n_yplots*(n_tintvs-i)-1,n_tintvs,n_yplots,0,1]
		n_src_already_plotted=0
		FOR j=0,N_ELEMENTS(disp)-1 DO BEGIN
			;plot_image,imgtess[*,*,disp[j],i],/VEL,xmar=[1,1],ymar=[1,1],xtickname=REPLICATE(' ',10),ytickname=REPLICATE(' ',10),/NOADJ
			plot_image,imgtess[*,*,disp[j],i],/VEL,xmar=[1,1],ymar=[1,1],xtickname=REPLICATE(' ',10),ytickname=REPLICATE(' ',10),/NOADJ,ticklen=1.,xgridstyle=1,ygridstyle=1			
			
			;oplot the contours of the sources, if they were found using this eband...
			FOR k=0,src_eband[j]-1 DO BEGIN
				cur_src=n_src_already_plotted	;cur_src=n_src-1-n_src_already_plotted				
				mytvellipse,src[cur_src].gra,src[cur_src].grb,src[cur_src].xmax,src[cur_src].ymax,src[cur_src].tet*180./!PI,/data
				XYOUTS,/DATA,src[cur_src].xmax+5,src[cur_src].ymax-5,strn(cur_src+1)
				IF KEYWORD_SET(STATS) THEN plot_label,/NOLINE,/DEV,[0.03,-k-2],strn(k+1)+': pos=['+strn(src[cur_src].xmax,format='(f10.1)')+','+strn(src[cur_src].ymax,format='(f10.1)')+']  size=['+strn(src[cur_src].gra,format='(f10.1)')+','+strn(src[cur_src].grb,format='(f10.1)')+']'
				n_src_already_plotted=n_src_already_plotted+1
			ENDFOR
		ENDFOR
		plot_label,/DEV,/NOLINE,[0.03,1],strn(i+1)+': '+anytim(tintvs[0,i],/ECS,/time)+'-'+anytim(tintvs[1,i],/ECS,/time),charsize=0.8
			
		; now, do the imspec proper:------------------------------------------------------------------------------------------------------------------
	IF n_src GT 0 THEN BEGIN
		sp=DBLARR(n_ebands,n_src)	;array to store spectra for each sources.
		FOR k=0L,n_src-1 DO BEGIN
			;sum every pixel inside the range, for every eband.
			FOR j=0,n_ebands-1 DO BEGIN
				sum=0d
				npix=0L
				FOR ix=0,xdim-1 DO BEGIN
					FOR iy=0,ydim-1 DO BEGIN
							;the following does not take into account the angle of the ellipse...
							;IF ( ((ix-src[k].xmax)/src[k].gra)^2 +((iy-src[k].ymax)/src[k].grb)^2 ) LE 1d THEN BEGIN
						IF (((ix-src[k].xmax)*cos(src[k].tet)+(iy-src[k].ymax)*sin(src[k].tet))/src[k].gra)^2 +((-(ix-src[k].xmax)*sin(src[k].tet)+(iy-src[k].ymax)*cos(src[k].tet))/src[k].grb)^2 LE 1d THEN BEGIN
							sum=sum+imgtess[ix,iy,j,i]
							npix=npix+1
						ENDIF
					ENDFOR
				ENDFOR				
				sp[j,k]=sp[j,k]+sum/(ebands[1,j]-ebands[0,j])/npix			;converts to ph/s/cm^2/asec^2/keV, and averages over source (hence, we get AVERAGED source INTENSITY)
			ENDFOR		
		ENDFOR
		
		;now, plot those spectra, and some fitting...
		IF imspecplotysize EQ 2 THEN !P.MULTI=[(n_yplots*(n_tintvs-i)-N_ELEMENTS(disp)-1)/2,n_tintvs,n_yplots/2,0,1]
		FOR k=0L,n_src-1 DO BEGIN
			IF k EQ 0 THEN color=!D.TABLE_SIZE-1 ELSE color=k
			IF k EQ 0 THEN PLOT,ebins,sp[*,k],/XLOG,/YLOG,xmar=[5,1],ymar=[4,0],yr=[1e-6,1e4],color=color,psym=1,xtit='Photon energy [keV]' $
			ELSE OPLOT,ebins,sp[*,k],color=color,psym=1			

				txt=strn(k+1)+': '
			;fitting an exponential... and displaying the results...
				;choosing bins where we actually have an above I50_min, and below 15 keV
				I50_min=1e-4	;0
				ss=WHERE((sp[*,k] GT I50_min) AND (ebins LT 15) AND (ebins GE 4))
				IF N_ELEMENTS(ss) GT 2 THEN coeff=hedc_imspec_robust_linefit(ebins[ss],ALOG(sp[ss,k]),yfit=yfit) ELSE coeff=-1
					IF N_ELEMENTS(coeff) GE 2 THEN BEGIN
					OPLOT,ebins[ss],exp(yfit),thick=1.0,color=color
					txt=txt+'T='+strn(-coeff[1],format='(f10.1)')+'keV  '
				ENDIF

			;fitting a power-law... and displaying the results...
				;choosing bins where we actually have an above I50_min, and inside the eband interval......
				I50_min=1e-4	;0
				ss=WHERE((sp[*,k] GT I50_min) AND (ebins GE fitEband[0]) AND (ebins LE fitEband[1]))
				IF N_ELEMENTS(ss) GT 2 THEN coeff=hedc_imspec_robust_linefit(ALOG10(ebins[ss]),ALOG10(sp[ss,k]),yfit=yfit) ELSE coeff=-1
					IF N_ELEMENTS(coeff) GE 2 THEN BEGIN
					OPLOT,ebins[ss],10^yfit,thick=2.0,color=color
					;txt=txt+'!7c!3='+strn(-coeff[1],format='(f10.1)')+' I!D50!N='+strn(10^(coeff[0]+coeff[1]*ALOG10(50)),format='(e10.1)')
					txt=txt+'!7c!3='+strn(-coeff[1],format='(f10.1)')
				ENDIF
				plot_label,/DEV,/NOLINE,[0.05,-k-1],txt,color=color
		ENDFOR		
	ENDIF ;//IF n_src GT 0
	PRINT,'.......% completed: '+strn(100.*(i+1)/n_tintvs,format='(f10.1)')
	ENDFOR ;//END OF MAIN LOOP
;-------------------------------------------------------------------------------------------------------------------------------------------------------------
;now, add a small margin to the left, with some more info...
	xmarginsize=30
	img=TVRD()
	psh_win,xmarginsize+basesize*n_tintvs,n_yplots*basesize	
	TV,img,xmarginsize,0
	XYOUTS,10,0.2*imspecplotysize*basesize,/DEVICE,"Average source `intensity' !C[ph s!U-1!N cm!U-2!N asec!U-2!N keV!U-1!N]",ORIENTATION=90
	FOR j=0,N_ELEMENTS(disp)-1 DO BEGIN
		XYOUTS,15,(0.35+N_ELEMENTS(disp)-j+imspecplotysize-1)*basesize,/DEVICE,strn(ebands[0,disp[j]],format='(f10.1)')+'-'+strn(ebands[1,disp[j]],format='(f10.1)')+' keV',ORIENTATION=90		
	ENDFOR
	XYOUTS,10,(n_yplots-0.8)*basesize,/DEVICE,"Corrected cts/s/det",ORIENTATION=90
	
	IF KEYWORD_SET(pngfile) THEN BEGIN
		TVLCT,r,g,b,/GET
		WRITE_PNG,pngfile,TVRD(),r,g,b
	ENDIF
END
;----------------------------------------------------------------------------------------------------------------------------------------

