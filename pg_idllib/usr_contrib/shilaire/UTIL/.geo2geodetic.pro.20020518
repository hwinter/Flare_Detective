;+
; NAME:
;       GEO2GEODETIC
;
; PURPOSE:
;	Converts from geographic (latitude, longitude, altitude) to geodetic
;	(latitude, longitude, altitude). The geodetic (or ellipsoidal) coordinate 
;	system takes into account the Earth's oblateness. In geographic coordinates,
;	the Earth is assumed a perfect sphere with a radius equal to its equatorial 
;	radius.
;
;	Latitudes and longitudes are expressed in degrees, altitudes in km.
;
;	REF: Stephen P.  Keeler and Yves Nievergelt, "Computing geodetic coordinates", SIAM Rev. Vol. 40, No. 2, pp. 300-309, June 1998
;	
;
; CALLING SEQUENCE:
;       ecoord=geo2geodetic(gcoord)
;
; INPUT:
;	gcoord = a 3-element array of geographic [latitude,longitude,altitude], or an array [3,n] of n such coordinates.
;
; KEYWORD INPUTS:
;	None
;
; OUTPUT:
;	a 3-element array of geodetic [latitude,longitude,altitude], or an array [3,n] of n such coordinates	
;
; COMMON BLOCKS:
;	None
;
; RESTRICTIONS:
;       None
;
; EXAMPLES:
;
;	IDL> geo=[90,0,0]	; North pole, altitude 0., in geographic coordinates
;	IDL> geod=geo2geodetic(geo)	; convert to equivalent geodetic coordinates
;	IDL> PRINT,geod		
;       89.999995       0.0000000       21.384586
;
; MODIFICATION HISTORY:
;	Written by Pascal Saint-Hilaire (shilaire@astro.phys.ethz.ch), May 2002
;
;-

;===================================================================================
FUNCTION geo2geodetic,gcoord

	Re=6378.137D	; equatorial radius
	Rp=6356.752D	; polar radius
	e=0.081819D	; eccentricity	= sqrt(Re^2-Rp^2)/Re
	;f=1/298.257D	; flattening = (Re-Rp)/Re
	
	glat=DOUBLE(gcoord[0,*])*!PI/180.
	glon=DOUBLE(gcoord[1,*])
	galt=DOUBLE(gcoord[2,*])
	
	x= (Re+galt) * cos(glat) * cos(glon)
	y= (Re+galt) * cos(glat) * sin(glon)
	z= (Re+galt) * sin(glat)	
	r=sqrt(x^2+y^2)
	
	elat=atan(1/(1-e^2)*z/r)
	beta=sqrt(1-(e*sin(elat))^2)
	ealt=sqrt((r - Re*cos(elat)/beta)^2+(z - (1-e^2)*Re*sin(elat)/beta)^2)
	elat=atan((1+ealt*beta/Re)*z/r/(1-e^2+ealt*beta/Re))

	elat=elat*180./!PI
	elon=glon
	
	RETURN,[elat,elon,ealt]
END
;===================================================================================
