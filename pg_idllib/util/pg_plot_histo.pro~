;+
; NAME:
;
; pg_plot_histo
;
; PURPOSE:
;
; computes and plots the distribution of an array
;
; CATEGORY:
;
; various utilities
;
; CALLING SEQUENCE:
;
; pg_plot_histo,y,min=min,max=max,binsize=binsize,xedges=xedges,histo=histo
;
; INPUTS:
;
; y: the input array
;
; OPTIONAL INPUTS:
;
; min: minimal value for the histogram DEFAULT: min(y)
; max: maximal value for the histogram DEFAULT: max(y)
; binsize: histogram bin size          DEFAULT: 0.1*(max(y)-min(y))
; nbins: can be used in alternative to binsize, number of bins. Is
;        ignored if binsize is present
; 
; KEYWORD PARAMETERS:
;
; xlog: if set, plots a logarithmic distribution. not all keywords
;       supported in this mode
;
; labelbins: if set, overplot the indices of the elements in each bin
; labelcharsize: label char size
; labelind: instead of writing the index i, writes labelind[i]
;
;
; OUTPUT:
;
; NONE
;
; OPTIONAL OUTPUTS:
;
; xedges: edges of the histogram
; histo: values of the histogram
;
;
; COMMON BLOCKS:
;
;
;
; SIDE EFFECTS:
;
;
;
; RESTRICTIONS:
;
;
;
; PROCEDURE:
;
;
;
; EXAMPLE:
; 
; a=[0.2,0.5,0.9,1.1,4.3,5.1,5.1] & min=0. & max= 6. & binsize=1.
; pg_plot_histo,a,min=min,max=max,binsize=binsize,yrange=[-1,4],xrange=[-1,7]
;
; AUTHOR:
;
; Paolo Grigis, Harvard-Smithsonian Center for Astrophysics
; pgrigis@cfa.harvard.edu
;
; MODIFICATION HISTORY:
;
; 07-JAN-2004 written P.G.
; 03-MAY-2004 corrected a bug which caused incorrect line
;             styles/thicknesses P.G.
; 14-JUL-2004 corrected a bug which appeared if x axis increased
;             toward the left P.G.
; 31-AUG-2004 added nbins and xlog keyword P.G.
; 02-SEP-2004 added label* keyword P.G.
; 07-SEP-2004 propagated histo/x_edges keyword correctly when /xlog is set P.G.
; 08-SEP-2004 added /noplot keyword P.G.
; 14-SEP-2004 corrected bug with xtitle display when in /xlog mode P.G.
; 21-SEP-2004 propagated correctly /overplot, linestyle and color
;             keywords when /xlog is set P.G. 
; 22-SEP-2004 fixed bug which caused redrawing the x-axis even if /overplot was
;             set P.G.
; 20-APR-2005 corrected xtitle bug introduced on 14-SEP-2004 P.G.
; 01-APR-2008 fixed min/max behavior in case they are negative and /xlog is set P.G.
;
;-

PRO pg_plot_histo,y,min=minn,max=maxx,binsize=binsize,nbins=nbins $
                 ,overplot=overplot,xlog=xlog,xrange=inxrange,xstyle=xstyle $
                 ,x_edges=x_edges,histo=histo,color=color,ylog=ylog $
                 ,nan=nan,linestyle=linestyle,thick=thick,charsize=charsize $
                 ,labelbins=labelbins,labcharsize=labcharsize $
                 ,labelind=labelind,debug=debug,noplot=noplot,xtitle=xtitle $
                 ,_extra=_extra

IF n_elements(minn) EQ 0 THEN minn=min(y)
IF n_elements(maxx) EQ 0 THEN maxx=max(y)
IF n_elements(inxrange) EQ 0 THEN xrange=[minn,maxx] ELSE xrange=inxrange


IF keyword_set(xlog) THEN BEGIN

   logy=alog(y)
   IF minn LE 0 THEN BEGIN
      print,'invalid min par for log distribution'
      minn=min(y)>1d-10
   ENDIF

   IF maxx LE 0 THEN BEGIN
      print,'invalid min par for log distribution'
      maxx=max(y)>1.
   ENDIF


   xrange=fcheck(inxrange,[minn,maxx])
   

   logmin=alog(minn)
   logmax=alog(maxx)

   extlog=pg_extend_range([logmin,logmax])
   logmin=extlog[0]
   logmax=extlog[1]
   

   IF (n_elements(binsize) EQ 0) AND (n_elements(nbins) NE 0) THEN $
      binsize=double(logmax-logmin)/(nbins>1)
  
;      binsize=fcheck(binsize,0.1d*(logmax-logmin))

      pg_plot_histo,logy,min=logmin,max=logmax,binsize=binsize,histo=histo $
         ,xrange=alog(xrange),xstyle=1B+4+8,thick=thick,x_edges=x_edges $
         ,ylog=ylog,labelbins=labelbins,charsize=charsize,noplot=noplot $
         ,_extra=_extra,labcharsize=labcharsize,labelind=labelind $
         ,overplot=overplot,linestyle=linestyle,color=color

      
      IF NOT (keyword_set(noplot) OR keyword_set(overplot)) THEN BEGIN 
         
         axis,xaxis=0,xrange=xrange,xstyle=1,xlog=1,charsize=charsize $
             ,xtitle=xtitle
         axis,xaxis=1,xrange=xrange,xstyle=1,xlog=1 $
             ,xtickname=replicate(' ',32),charsize=charsize

         pg_plot_histo,logy,min=logmin,max=logmax,binsize=binsize $
             ,xstyle=1,/overplot,thick=thick,xrange=alog(xrange),ylog=ylog $
             ,linestyle=linestyle,color=color          

      ENDIF

  
      RETURN
   ENDIF


IF (n_elements(binsize) EQ 0) AND (n_elements(nbins) NE 0) THEN binsize=double(maxx-minn)/(nbins>1)

binsize=fcheck(binsize,0.1d*(maxx-minn))

;IF NOT exist(nbins) THEN $
nbins=ceil((maxx-minn)/binsize)


x_edges=transpose(reform(minn+[findgen(nbins)*binsize, $
             findgen(nbins)*binsize+binsize],nbins,2))
edge_products,x_edges,mean=x

histo=histogram(y,binsize=binsize,min=minn,max=minn+nbins*binsize,nan=nan $
               ,reverse_indices=ri)

;IF keyword_set(debug) THEN stop

IF keyword_set(noplot) THEN RETURN

IF (NOT keyword_set(overplot)) THEN BEGIN 
   plot,x,histo,psym=10,ylog=ylog,linestyle=linestyle,thick=thick $
               ,xrange=xrange,_extra=_extra,xstyle=xstyle,charsize=charsize $
               ,xtitle=xtitle
   IF n_elements(color) NE 0 THEN $
     oplot,x,histo,psym=10,color=color $
          ,linestyle=linestyle,thick=thick

   IF keyword_set(labelbins) THEN BEGIN

      labcharsize=fcheck(labcharsize,1.)

      labbin=strarr(nbins)

      FOR i=0,nbins-1 DO BEGIN
         IF ri[i] EQ ri[i+1] THEN BEGIN
            labbin[i]=''
         ENDIF ELSE  BEGIN 
            ellist=ri[ri[i]:ri[i+1]-1]
            IF exist(labelind) THEN ellist=labelind[ellist]
            string=''
            FOR j=0,n_elements(ellist)-1 DO $
              string=string+strtrim(string(ellist[j]),2)+'!C'
            labbin[i]=string
         ENDELSE
      ENDFOR

      ylabel=max(!y.crange)-0.05*(max(!y.crange)-min(!y.crange))
      FOR i=0,nbins-1 DO xyouts,x[i],ylabel,labbin[i],charsize=labcharsize

   ENDIF


ENDIF ELSE $
      oplot,x,histo,psym=10,color=color,linestyle=linestyle,thick=thick

IF keyword_set(ylog) THEN zero=(machar()).xmin ELSE zero=0.
;needed for plots with y log scale, otherwise it won't plot
;correctly!


;plot histogram boundaries!
oplot,[x_edges[0,0],x_edges[0,0]],[zero,histo[0]],color=color $
     ,linestyle=linestyle,thick=thick
oplot,[x_edges[0,0],x_edges[1,0]],[histo[0],histo[0]],color=color $
     ,linestyle=linestyle,thick=thick
oplot,[min(!X.crange),x_edges[0,0]],[0,0],color=color $
     ,linestyle=linestyle,thick=thick
oplot,[x_edges[1,nbins-1],x_edges[1,nbins-1]],[zero,histo[nbins-1]] $
     ,color=color,linestyle=linestyle,thick=thick
oplot,[x_edges[0,nbins-1],x_edges[1,nbins-1]],[histo[nbins-1],histo[nbins-1]] $
     ,color=color,linestyle=linestyle,thick=thick
oplot,[x_edges[1,nbins-1],max(!X.crange)],[0,0],color=color $
     ,linestyle=linestyle,thick=thick



END
